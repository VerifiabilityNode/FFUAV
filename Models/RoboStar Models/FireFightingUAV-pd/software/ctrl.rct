// The main controller (NUC).

diagram ctrl

// Importing platform interfaces
import gcs::*
import m600::*
import mlx90640::*
import gimbal::*
import pump::*
import rsd435i::*

// Importing communications
import comm::*
import controller_comm::*

// Importing state machines
import fire::*
import flight::*
import planning::*
import spray::*
import visual::*

controller MainCtrl {
	//
	// M600 API operations
	//
	
	requires M600Flight
	
	//
	// Connected peripherals
	//
	
	requires GCS
	requires MLX90640
	requires RealSenseD435i
		
	uses M600FlightEvents
	uses M600TelemetryEvents
	uses M600BatteryTelemetryEvents
	uses MLX90640Events
	uses RealSenseD435iEvents
	
	uses GCSMissionEvents
	uses GCSControlEvents 
	
	//
	// Inter-controller events
	//
	uses InterCtrlSprayAimEvents


	//
	// State machines
	//
	
	sref CPlanning = Planning
	sref CFireDetect = FireDetect
	sref CVisualNav = VisualNav
	sref CFlight = Flight
	sref CBatteryMonitor = BatteryMonitor
	sref CSprayAim = SprayAim
	sref CWaterMonitor = WaterMonitor
	
	//
	// Bus connections
	//
	// These represent a static encoding of 1-to-many pub-sub connections.
	// While the sending order at the pub side is fixed, the semantics of async
	// communications captures the fact that the messages can arrive in any
	// order at subs.
	//
	
	// Fire
	connection CFireDetect     on firePOut                  to CPlanning       on fireStatusIn	 (_async)
	connection CFireDetect     on fireSOut                  to CSprayAim       on fireIn         (_async)
    connection CFireDetect     on fireVOut                  to CVisualNav      on fireIn         (_async)
    // Spray
    connection CPlanning       on sprayMOut                 to CWaterMonitor   on sprayIn        (_async)
    connection CPlanning       on spraySOut                 to CSprayAim       on sprayIn        (_async)
    // Water low
    connection CWaterMonitor   on waterLowPOut              to CPlanning       on waterIn        (_async)
    connection CWaterMonitor   on waterLowSOut              to CSprayAim       on waterIn        (_async)
    // Visual nav
    connection CVisualNav      on vnavFOut                  to CFlight         on vnavIn         (_async)
    connection CVisualNav      on vnavPOut                  to CPlanning       on vnavIn         (_async)
	
	//
	// Connections between state machines
	//
	
	// FireVisualEvents
	connection CFireDetect     on wallMetrics               to CVisualNav      on wallMetrics    (_async)
	// VisualPlanningEvents (added 2022-05-24)	
	connection CPlanning       on vnavEnable                to CVisualNav      on vnavEnable     (_async)
	// FlightPlanningEvents
	connection CPlanning       on navCommand                to CFlight         on navCommand     (_async)
	connection CPlanning       on waypoint                  to CFlight         on waypoint       (_async)
	connection CFlight         on uavStatus                 to CPlanning       on uavStatus      (_async)
	// GimbalPlanningEvents
	connection CSprayAim       on gimbalStatus              to CPlanning       on gimbalStatus   (_async)	
	// BatteryPlanningEvents
	connection CBatteryMonitor on batteryStatus             to CPlanning       on batteryStatus  (_async)

	//
	// Connections from Ctrl state machines to ACtrl
	//
	
	// InterCtrlSprayAimEvents
	connection CSprayAim       on pump                      to MainCtrl        on pump
	connection CSprayAim       on gimbalReq                 to MainCtrl        on gimbalReq
	connection MainCtrl        on gimbalAck                 to CSprayAim       on gimbalAck

	//
	// Connections from platform to state machines
	//

	// M600FlightEvents
	connection MainCtrl        on fcTakeoffRet              to CFlight         on fcTakeoffRet
	connection MainCtrl        on fcGoHomeRet               to CFlight         on fcGoHomeRet
	connection MainCtrl        on fcLandRet                 to CFlight         on fcLandRet
	connection MainCtrl        on fcControlAuth             to CFlight         on fcControlAuth
	connection MainCtrl        on fcWaypointFinishedMission to CFlight         on fcWaypointFinishedMission
	connection MainCtrl        on fcMoveRelativeRet         to CFlight         on fcMoveRelativeRet
	// M600TelemetryEvents (empty)
	// M600BatteryTelemetryEvents
	connection MainCtrl        on batteryInfo               to CBatteryMonitor on batteryInfo
	// MLX90640Events
	connection MainCtrl        on mlxHeatmap                to CFireDetect     on mlxHeatmap
	// RealSenseD435iEvents
	connection MainCtrl        on rsDepthFrame              to CFireDetect     on rsDepthFrame
	connection MainCtrl        on rsRgbFrame                to CFireDetect     on rsRgbFrame
	// GCSMissionEvents
	connection CPlanning       on missionStatus             to MainCtrl        on missionStatus
}
