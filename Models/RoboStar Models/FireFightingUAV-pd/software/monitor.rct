// Battery and water monitors.
diagram monitor

// Battery monitor.
//
// Subscribes continuously to the battery topic and signals if the battery goes
// below mission or safety criticality thresholds.
stm BatteryMonitor {
	uses BatteryPlanningEvents
	uses M600BatteryTelemetryEvents
	
	const BATTERY_MISSION_THRESHOLD: nat, BATTERY_SAFETY_THRESHOLD: nat, BATTERY_POLL_DELAY: nat
	var battery: Battery

	initial I
	
	state Polling {
	}
		
	transition Init { from I to Polling }
	
	junction CheckM
	junction CheckS
	junction Waiting
	
	transition Polled { from Polling to CheckM trigger batteryState?battery }
	
	transition BatteryMO { from CheckM to Waiting condition battery.percentage >  BATTERY_MISSION_THRESHOLD
		                                          action batteryStatus!BatteryStatus::Ok
	}
	transition BatteryML { from CheckM to CheckS  condition battery.percentage <= BATTERY_MISSION_THRESHOLD
	}
	
	transition BatterySO { from CheckS to Waiting condition battery.percentage >  BATTERY_SAFETY_THRESHOLD
		                                          action batteryStatus!BatteryStatus::MissionCritical
	}
	transition BatterySL { from CheckS to Waiting condition battery.percentage <= BATTERY_SAFETY_THRESHOLD
		                                          action batteryStatus!BatteryStatus::SafetyCritical
	}

	transition Wait { from Waiting to Polling action wait(BATTERY_POLL_DELAY) }
}

// Water level monitor.
//
// Subscribes to the spray status topic, switching between 'on' and 'off'
// states.  While 'on' it periodically drains a water amount; when it goes to
// zero, it publishes the fact that the water level has gone low and finishes.
stm WaterMonitor {
	uses SprayEvents
	
	// Publishing water topic (added 2022-08-31, refined 2022-09-05)
	event waterStatusOut : WaterStatus
	
	const INITIAL_WATER_LEVEL: real, WATER_FLOW: real
	const TIME_PER_UNIT: nat = 1

	var pstatus : PumpCommand
	var water: real = INITIAL_WATER_LEVEL
	
	initial I
	
	state Active {
		initial I
	
		state Off {
			initial I
			state OffCheck {}
			
			transition Init { from I to OffCheck }
			transition Read { from OffCheck to OffCheck trigger sprayIn?pstatus }
		transition t0 {
				from OffCheck
				to OffCheck
				condition sinceEntry ( Active::Off::OffCheck ) >= 1
				action 
			waterStatusOut ! WaterStatus :: Ok
			}
		}
		
		state On {
			initial I
			state OnCheck {}
			
			transition Init { from I to OnCheck }
			transition Read { from OnCheck to OnCheck trigger sprayIn?pstatus }
		transition t0 {
				from OnCheck
				to OnCheck
				condition sinceEntry ( Active::On::OnCheck ) >= 1
				action waterStatusOut ! WaterStatus :: Ok
			}
		}

		transition Init { from I to Off }

	
		transition SwitchOn { from Off to On condition pstatus == PumpCommand::On /\ 0 < water }
		transition SwitchOff { from On to Off condition pstatus == PumpCommand::Off /\ sinceEntry(On) < TIME_PER_UNIT /\ 0 < water }
		
		transition Tick { from On to On condition TIME_PER_UNIT <= sinceEntry(On) /\ 0 < water action water = water - WATER_FLOW }
	}
	state s0 {
	}
	
	transition Init { from I to Active }

	transition Out { from Active to s0 condition water <= 0
	}
	transition t0 {
		from s0
		to s0
		condition sinceEntry ( s0 ) >= 1
		action 
	
	waterStatusOut ! WaterStatus :: Low
	}
}

